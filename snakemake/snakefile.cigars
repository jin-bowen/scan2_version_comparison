# vim: syntax=python

rule cigar_scatter:
    input:
        sites="call_mutations/mmq60.tab.gz",
        sitesidx="call_mutations/mmq60.tab.gz.tbi",
        bam=lambda wildcards: config['bam_map'][wildcards.sample]
    output:
        tab=temp("call_mutations/{sample}/cigars.{chr}.tab")
    benchmark:
        "call_mutations/{sample}/benchmark_cigars.{chr}.txt"
    params:
        chrom="{chr}"
    resources:
        mem=250
    shell:
        """
        {config[scripts]}/count_cigars.py {input.bam} {input.sites} {output.tab} --chrom {params.chrom}
        """


# Concatenate and zip/index
rule cigar_gather:
    input:
        lambda wildcards:
            expand("call_mutations/{sample}/cigars.{chr}.tab",
                    sample=wildcards.sample,
                    chr=config['chrs'])
    output:
        tab="call_mutations/{sample}/cigars.tab.gz",
        idx="call_mutations/{sample}/cigars.tab.gz.tbi"
    benchmark:
        "call_mutations/{sample}/benchmark_cigar_gather.txt"
    resources:
        mem=500
    shell:
        """
        (head -1 {input[0]} ; cat {input} | grep -v '^#') | bgzip -c > {output.tab}
        tabix -p vcf -S 1 {output.tab}
        """
