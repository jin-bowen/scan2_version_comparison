# vim: syntax=python

rule pregenotype:
    input:
        inttab="call_mutations/integrated_table.tab.gz",
        inttabidx="call_mutations/integrated_table.tab.gz.tbi",
        sccigars="call_mutations/{sample}/cigars.tab.gz",
        sccigarsidx="call_mutations/{sample}/cigars.tab.gz.tbi",
        bulkcigars="call_mutations/%s/cigars.tab.gz" % config['bulk_sample'],
    output:
        cigardata=temp("call_mutations/{sample}/cigardata.tab"),
        cigardatagz="call_mutations/{sample}/cigardata.tab.gz",
        cigardataidx="call_mutations/{sample}/cigardata.tab.gz.tbi",
    log:
        "call_mutations/{sample}/call_mutations.log"
    threads: 1
    params:
        sc_sample="{sample}",
        bulk_sample=config['bulk_sample'],
        genome=config['genome']
    resources:
        mem=8000
    benchmark:
        "call_mutations/{sample}/benchmark_pregenotyping.txt"
    shell:
        """
        {config[scripts]}/pregenotyping.R \
            {params.sc_sample} {params.bulk_sample} {input.inttab} \
            {input.sccigars} {input.bulkcigars} {params.genome} \
            {output.cigardata} >& {log}
        """


rule genotype:
    input:
        inttab="call_mutations/integrated_table.tab.gz",
        inttabidx="call_mutations/integrated_table.tab.gz.tbi",
        fits="ab_model/{sample}/fits.rda",
        sccigars="call_mutations/{sample}/cigars.tab.gz",
        sccigarsidx="call_mutations/{sample}/cigars.tab.gz.tbi",
        bulkcigars="call_mutations/%s/cigars.tab.gz" % config['bulk_sample'],
        bulkcigarsidx="call_mutations/%s/cigars.tab.gz.tbi" % config['bulk_sample'],
        cigardata="call_mutations/{sample}/cigardata.tab.gz",
        cigardataidx="call_mutations/{sample}/cigardata.tab.gz.tbi",
        dptab="depth_profile/{sample}_depth_table.rda"
    output:
        "call_mutations/{sample}/scan2_object.rda"
    log:
        "call_mutations/{sample}/call_mutations.log"
    params:
        sc_sample="{sample}",
        # bit of a hack here: assumes the script is run from the root of the
        # working directory (i.e., the same as is assumed for input/output
        # file names).
        config_yaml='scan.yaml'
    threads: config['genotype_n_cores']
    resources:
        mem=1500*config['genotype_n_cores']
    benchmark:
        "call_mutations/{sample}/benchmark_genotype.txt"
    shell:
        """
        {config[scripts]}/call_mutations.R \
           {params.sc_sample} {params.config_yaml} \
           {input.inttab} {input.fits} \
           {input.sccigars} {input.bulkcigars} \
           {input.cigardata} {input.dptab} \
           {output} {threads} >& {log}
        """
